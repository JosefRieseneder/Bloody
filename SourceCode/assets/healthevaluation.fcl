FUNCTION_BLOCK healthevaluation

VAR_INPUT
	systolicPressure : REAL;
	diastolicPressure : REAL;
	bmi : REAL;
END_VAR

VAR_OUTPUT
	health : REAL;
END_VAR

FUZZIFY bmi
	TERM severelyUnderweight := (0, 1) (16, 0) ;
	TERM underweight := (13, 0) (17.25, 1) (18.5, 0) ;
	TERM normal := (17.5, 0) (21.75, 1) (27, 0) ;
	TERM obese := (25, 0) (27.5, 1) (33, 0) ;
	TERM severelyObese := (30, 0) (45, 1) ;
END_FUZZIFY

FUZZIFY systolicPressure
	TERM hypotension := (0, 1) (92, 0) ;
	TERM normal := (88, 0) (105, 1) (122, 0) ;
	TERM prehypertension := (118, 0) (130, 1) (142, 0) ;
	TERM sOneHypertension := (138, 0) (150, 1) (162, 0) ;
	TERM sTwoHypertension := (158, 0) (170, 1) (182, 0) ;
	TERM hypertension := (178, 0) (200, 1) ;
END_FUZZIFY

FUZZIFY diastolicPressure
	TERM hypotension := (0, 1) (62, 0) ;
	TERM normal := (58, 0) (70, 1) (82, 0) ;
	TERM prehypertension := (78, 0) (85, 1) (92, 0) ;
	TERM sOneHypertension := (88, 0) (95, 1) (102, 0) ;
	TERM sTwoHypertension := (98, 0) (105, 1) (112, 0) ;
	TERM hypertension := (108, 0) (128, 1) ;
END_FUZZIFY

DEFUZZIFY health
	TERM miserable := (-35, 0) (-30, 1) (-25, 0) ;
	TERM prettyBad := (-25, 0) (-20, 1) (-15, 0) ;
	TERM bad := (-15, 0) (-10, 1) (-5, 0) ;
	TERM average := (-5, 0) (0, 1) (5, 0) ;
	TERM good := (5, 0) (10, 1) (15, 0) ;
	TERM veryGood := (15, 0) (20, 1) (25, 0) ;
	TERM excellent := (25, 0) (30, 1) (35, 0) ;
	METHOD : COG ;
	DEFAULT := 0 ;
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN ;
	ACT : MIN ;
	ACCU : MAX ;

	RULE 1 : IF bmi IS normal
		THEN health IS excellent;
	RULE 2 : IF bmi IS underweight
		OR bmi IS obese
		THEN health IS average;
	RULE 3 : IF bmi IS severelyUnderweight
		OR bmi IS severelyObese
		THEN health IS prettyBad;

	RULE 4 : IF systolicPressure IS normal
		AND diastolicPressure IS normal
		THEN health IS excellent;

	RULE 5 : IF systolicPressure IS normal
		AND diastolicPressure IS hypotension
		OR diastolicPressure IS prehypertension
		THEN health IS veryGood;

	RULE 6 : IF diastolicPressure IS normal
		AND systolicPressure IS hypotension
		OR systolicPressure IS prehypertension
		THEN health IS veryGood;

	RULE 7 : IF diastolicPressure IS sOneHypertension
		OR systolicPressure IS sOneHypertension
		THEN health IS bad;

	RULE 8 : IF diastolicPressure IS sTwoHypertension
		OR systolicPressure IS sTwoHypertension
		THEN health IS prettyBad;

	RULE 9 : IF diastolicPressure IS hypertension
		OR systolicPressure IS hypertension
		THEN health IS miserable;
END_RULEBLOCK

END_FUNCTION_BLOCK
